// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/05/Memory.hdl

/**
 * The complete address space of the Hack computer's memory,
 * including RAM and memory-mapped I/O. 
 * The chip facilitates read and write operations, as follows:
 *     Read:  out(t) = Memory[address(t)](t)
 *     Write: if load(t-1) then Memory[address(t-1)](t) = in(t-1)
 * In words: the chip always outputs the value stored at the memory 
 * location specified by address. If load==1, the in value is loaded 
 * into the memory location specified by address. This value becomes 
 * available through the out output from the next time step onward.
 * Address space rules:
 * Only the upper 16K+8K+1 words of the Memory chip are used. 
 * Access to address>0x6000 is invalid. Access to any address in 
 * the range 0x4000-0x5FFF results in accessing the screen memory 
 * map. Access to address 0x6000 results in accessing the keyboard 
 * memory map. The behavior in these addresses is described in the 
 * Screen and Keyboard chip specifications given in the book.
 */

CHIP Memory {
    IN in[16], load, address[15];
    OUT out[16];

    PARTS:
    // check if the address is within the range of RAM address values
    Not(in=address[14], out=leadingzero);
    And(a=load, b=leadingzero, out=ramload);
    RAM16K(in=in, load=ramload, address=address[0..13], out=ramout);

    // check if the address is within the range of the screen address values
    Not(in=address[13], out=secondzero);
    And(a=address[14], b=secondzero, out=scrntemp);
    And(a=scrntemp, b=load, out=scrnload);

    // check if the address is a keyboard address
    Screen(in=in, load=scrnload, address=address[0..12], out=scrnout);
    Keyboard(out=kbdout);

    // using And and Mux16 gates, load the value into the corresponding
    // register and output the value
    And(a=address[13], b=address[14], out=keyboard);
    Mux16(a=ramout, b=scrnout, sel=scrntemp, out=t1);
    Mux16(a=t1, b=kbdout, sel=keyboard, out=out);
}
